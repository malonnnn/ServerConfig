marry:/marry $vic = >>>

    @player = player()
    @vic = $vic

    if(has_permission(@player,'is.married')){
        @spouse = get_value(@player.'spouse')
        tmsg(@player,color(red).'Sorry, you are already married to '.@spouse.'!')
        die()
    }

    if(has_permission(@vic,'is.married')){
        @spouse = get_value(@vic.'spouse')
        tmsg(@player,color(red).'Sorry, they are already married to '.@spouse.'!')
        die()
    }

    @vic = player(@vic)

    if(@player == @vic){
        tmsg(@player,color(red).'You cannot marry yourself!')
        die()
    }

    store_value(@player.'spouse',@vic)
    if(get_value(@vic.'spouse') != null && get_value(@vic.'spouse') == @player && @player != @vic){
        broadcast("§c❤❤❤§a ".@player." §6and§a ".@vic." §6just got married! §c❤❤❤")
        runas(~console,'/pex user '.@player.' add is.married')
        runas(~console,'/pex user '.@vic.' add is.married')
    }else{
        tmsg(@player,color(red).'Once your crush does '.color(green).'/marry '.@player.color(red).' then you two will be officially married!')

    }
<<<

divorce:/divorce $plr1 $plr2 = >>>

    @plr1 = $plr1
    @plr2 = $plr2

    @plr1 = player(@plr1)
    @plr2 = player(@plr2)

    runas(~console,'/pex user '.@plr1.' remove is.married')
    runas(~console,'/pex user '.@plr2.' remove is.married')

    store_value(@plr1.'spouse','')
    store_value(@plr2.'spouse','')
    
    tmsg(@plr1,color(red).'You are now officially single!')
    tmsg(@plr2,color(red).'You are now officially single!')

<<<
